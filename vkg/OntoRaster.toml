title="OntoRaster Demonstration"

[[tabGroups]]

name="Sample RasSPARQL Queries"

[[tabGroups.tabs]]

name="Q1. Get Dimension"
query="""
#####################################################################
#### Query 1. Find the dimension of user-specific Raster dataset ####
#####################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?rasterName ?dimension {
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, '')) # Also try 'Munich', 'Sweden', 'Bavaria', 'Tyrol'
BIND (rasdb:rasDimension(?rasterName) AS ?dimension)
}
"""

[[tabGroups.tabs]]

name="Q2. Cell Operation"
query="""
#################################################################################################################
#### Query 2. Perform element-wise operation over raster array cells with user-specific operator and operand ####
#################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?raster rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Sweden'))
BIND ('*' AS ?operator) 
BIND (0.02 AS ?operand)
BIND ('2022-04-18T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasCellOp(?timeStamp, ?operator, ?operand, ?rasterName) AS ?answer)
}
"""

[[tabGroups.tabs]]

name="Q3. Spatial Average"
query="""
###################################################################################################
#### Query 3. Find spatial average value over a user-specific vector Region and Raster dataset ####
###################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'München') # also try with Deggendorf, Bayreuth, Würzburg etc.         
FILTER (CONTAINS(?rasterName, 'Bavaria'))
BIND ('2023-09-04T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?regionWkt, ?rasterName) AS ?answer)
} 
"""

[[tabGroups.tabs]]

name="Q4. Spatial Maximum"
query="""
###################################################################################################
#### Query 4. Find spatial maximum value over a user-specific vector Region and Raster dataset ####
###################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'Göteborg') # also try with Linköping, Uppsala, Göteborg, Stockholm etc.
FILTER (CONTAINS(?rasterName, 'Sweden'))
BIND ('2022-08-24T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialMaximum(?timeStamp, ?regionWkt, ?rasterName) AS ?answer)
} LIMIT 1 
"""

[[tabGroups.tabs]]

name="Q5. Spatial Minimum"
query="""
###################################################################################################
#### Query 5. Find spatial minimum value over a user-specific vector Region and Raster dataset ####
###################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'Bolzano') # also try with Castelrotto, Sarentino, Fortezza  etc.
FILTER (CONTAINS(?rasterName, 'Tyrol'))
BIND ('2023-03-03T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialMinimum(?timeStamp, ?regionWkt, ?rasterName) AS ?answer)
} 
"""

[[tabGroups.tabs]]

name="Q6. Temporal Average"
query="""
###########################################################################################################################
#### Query 6. Find temporal average value over a user-specific vector Region and Raster dataset between two timestamps ####
###########################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'Cham')
FILTER (CONTAINS(?rasterName, 'Bavaria'))
BIND ('2023-06-15T00:00:00+00:00'^^xsd:dateTime AS ?startTimeStamp)
BIND ('2023-07-21T00:00:00+00:00'^^xsd:dateTime AS ?endTimeStamp)
BIND (rasdb:rasTemporalAverage(?startTimeStamp, ?endTimeStamp , ?regionWkt, ?rasterName) AS ?answer)
} LIMIT 1 
"""

[[tabGroups.tabs]]

name="Q7. Temporal Maximum"
query="""
###########################################################################################################################
#### Query 7. Find temporal maximum value over a user-specific vector Region and Raster dataset between two timestamps ####
###########################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'Göteborg') 
FILTER (CONTAINS(?rasterName, 'Sweden'))
BIND ('2022-04-05T00:00:00+00:00'^^xsd:dateTime AS ?startTimeStamp)
BIND ('2022-06-19T00:00:00+00:00'^^xsd:dateTime AS ?endTimeStamp)
BIND (rasdb:rasTemporalMaximum(?startTimeStamp, ?endTimeStamp , ?regionWkt, ?rasterName) AS ?answer)
} LIMIT 1 
"""

[[tabGroups.tabs]]

name="Q8. Temporal Minimum"
query="""
###########################################################################################################################
#### Query 8. Find temporal minimum value over a user-specific vector Region and Raster dataset between two timestamps ####
###########################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'Sarentino') 
FILTER (CONTAINS(?rasterName, 'Tyrol'))
BIND ('2023-07-15T00:00:00+00:00'^^xsd:dateTime AS ?startTimeStamp)
BIND ('2023-07-21T00:00:00+00:00'^^xsd:dateTime AS ?endTimeStamp)
BIND (rasdb:rasTemporalMinimum(?startTimeStamp, ?endTimeStamp , ?regionWkt, ?rasterName) AS ?answer)
} 

"""

[[tabGroups.tabs]]

name="Q9. Clip Raster"
query="""
#####################################################################################################################
#### Query 9. Clip a portion of the raster based on the shape of vector Region over user-specific raster dataset ####
#####################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?region a :Region .
?region rdfs:label ?regionName .
?region geo:asWKT ?regionWkt .
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (?regionName = 'Castelrotto') 
FILTER (CONTAINS(?rasterName, 'Tyrol')) 
BIND ('2023-09-24T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasClipRaster(?timeStamp, ?regionWkt, ?rasterName) AS ?answer) 
}

"""

[[tabGroups.tabs]]

name="Q10. Clip Raster Custom Region"
query="""
#################################################################################################################################
#### Query 10. Clip a portion of the raster based on the shape of the custom vector Region over user-specific raster dataset ####
#################################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?answer {
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Bavaria')) 
BIND ('POLYGON((11.324157714843748 48.29050321714061,11.911926269531248 48.279537342260085,11.88995361328125 48.01932418480118,11.340637207031248 48.01564978668938,11.324157714843748 48.29050321714061))' AS ?customRegionWkt)
BIND ('2023-07-24T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasClipRasterAnyGeom(?timeStamp, ?customRegionWkt, ?rasterName) AS ?answer) 
}
"""

[[tabGroups]]

name="Munich (D2G2)"

[[tabGroups.tabs]]

name="Q11. Get Districts (in 2.5 sec)"
query="""
###########################################################################################
#### Query 11. Find all districts of Munich where elevation is more than 515 meters    ####
###########################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?distName ?elevation {
?region a :District ; rdfs:label ?distName ; geo:asWKT ?distWkt .
?gridCoverage a :Raster ; rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Elevation'))
BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?distWkt, ?rasterName) AS ?elevation)
FILTER(?elevation > 515)
} 

#SELECT ?distName ?elevation ?distWkt ?distWktColor {
#?region a :District .
#?region rdfs:label ?distName .
#?region geo:asWKT ?distWkt .
##BIND(?distName AS ?distWktLabel)
##BIND('#008AFFF0' AS ?distWktColor)
#?gridCoverage a :Raster .
#?gridCoverage rasdb:rasterName ?rasterName .
#FILTER (CONTAINS(?rasterName, 'Elevation'))
#BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
#BIND (rasdb:rasSpatialAverage(?timeStamp, ?distWkt, ?rasterName) AS ?elevation)
#FILTER(?elevation > 515) .
#  BIND( 
#    IF(?elevation < 525, "blue" , 
#    IF(?elevation < 535, "#008AFF",
#    IF(?elevation < 545, "magenta",
#    IF(?elevation < 555, "red",
#    IF(?elevation < 565, "#C60000",
#    "black")))))
#    AS ?distWktColor).
#} 
#GROUP BY ?distName ?elevation ?distWkt ?distWktColor

"""

[[tabGroups.tabs]]

name="Q12. Get SubDistricts (in 16.34 sec)"
query="""
##############################################################################################
#### Query 12. Find all sub-districts of Munich where elevation is between 510-520 meters ####
##############################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?subDistName ?elevation ?subDistWkt ?subDistWktColor {
?region a :SubDistrict .
?region rdfs:label ?subDistName .
?region geo:asWKT ?subDistWkt .
BIND('#008AFFF0' AS ?subDistWktColor)
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Elevation'))
BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?subDistWkt, ?rasterName) AS ?elevation)
FILTER(?elevation > 510 && ?elevation < 520)
} 

"""

[[tabGroups.tabs]]

name="Q13. Clip Raster Custom Region (in 10.34 sec)"
query="""
############################################################################################################################################
#### Query 13. Clip and return a portion of the raster based on the shape of the custom vector Region over user-specific raster dataset ####
############################################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	<http://www.opengis.net/ont/geosparql#>
PREFIX rasdb:	<https://github.com/aghoshpro/RasterDataCube/>

SELECT ?clipped ?customRegionWkt {
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Munich_MODIS_Temperature_1km')) 
BIND ('POLYGON((11.548455354852285 48.14904359943597,11.627292392870094 48.147177113486606,11.58380485801181 48.11696471831948,11.546675164347198 48.119171832113835,11.548455354852285 48.14904359943597))' AS ?customRegionWkt)
BIND ('2022-01-01T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasClipRasterAnyGeom(?timeStamp, ?customRegionWkt, ?rasterName) AS ?clipped) 
}

"""

[[tabGroups.tabs]]

name="Q14. Get Churches (in 5.687 sec)"
query="""
###########################################################################################
#### Query 14. List all the churches in Munich where elevation is less than 520 meters ####
###########################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?bldgName ?distName ?elevation {
?region a :District .
?region rdfs:label ?distName .
?region geo:asWKT ?distWkt .
?building a lgdo:Church .
?building rdfs:label ?bldgName .
?building geo:asWKT ?bldgWkt .
FILTER (geof:sfWithin(?bldgWkt, ?distWkt))
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Elevation'))
BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?distWkt, ?rasterName) AS ?elevation)
FILTER(?elevation < 520)
} 

"""

[[tabGroups.tabs]]

name="Q15. Get Churches (in 7.665 sec)"
query="""
###########################################################################################
#### Query 15. List all the churches in Munich where elevation is more than 500 meters
###########################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?bldgName ?distName ?elevation ?bldgLat ?bldgLon ?distWkt ?distWktColor ?bldgWkt ?bldgWktColor {
?region a :District ; rdfs:label ?distName ; geo:asWKT ?distWkt .
?building a lgdo:Church ; rdfs:label ?bldgName ; geo:asWKT ?bldgWkt .
BIND('#008AFF5C' AS ?distWktColor)
BIND('red' AS ?bldgWktColor)
#BIND(geof:latitude("?bldgWkt"^^geo:wktLiteral) AS ?bldgLat)
#BIND(geof:longitude("?bldgWkt"^^geo:wktLiteral) AS ?bldgLon)
FILTER (geof:sfWithin(?bldgWkt, ?distWkt))
?gridCoverage a :Raster ; rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Elevation'))
BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialMinimum(?timeStamp, ?distWkt, ?rasterName) AS ?elevation)
FILTER(?elevation < 520)
} 

#SELECT ?bldgName ?bldgLat ?bldgLong WHERE {
#  ?building a lgdo:Church ; rdfs:label ?bldgName ; geo:asWKT ?bldgWkt .
#  
#  # Calculate centroid and extract latitude/longitude
#  BIND(geo:hasCentroid(?bldgWkt) AS ?centroidWkt)
#  BIND(geof:latitude(?centroidWkt) AS ?bldgLat)
#  BIND(geof:longitude(?centroidWkt) AS ?bldgLong)
#}
#

"""

[[tabGroups.tabs]]

name="Q16. Get Universities (in 6.82 sec)"
query="""
###############################################################################################
#### Query 16. List all the universities in Munich where elevation is more than 500 meters ####
###############################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?bldgName ?distName ?elevation ?distWkt ?distWktColor ?bldgWkt ?bldgWktColor {
?region a :District . # SubDistrict
?region rdfs:label ?distName .
?region geo:asWKT ?distWkt .
BIND('#008AFF5C' AS ?distWktColor)
?building a lgdo:University .
?building rdfs:label ?bldgName .
?building geo:asWKT ?bldgWkt .
?bldg geo:asWKT ?bldgWkt .
BIND('red' AS ?bldgWktColor)
FILTER (geof:sfWithin(?bldgWkt, ?distWkt))
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Elevation'))
BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?distWkt, ?rasterName) AS ?elevation)
FILTER(?elevation > 500)
} 

"""

[[tabGroups.tabs]]

name="Q17. Get Residential (in 135.8 sec)"
query="""
##########################################################################################################################
#### Query 17. List all the residential and respective subdistricts in Munich where elevation is more than 520 meters ####
##########################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?subDistName ?elevation ?subDistWkt ?subDistWktColor ?bldgWkt ?bldgWktColor { # bldgName
?region a :SubDistrict . 
?region rdfs:label ?subDistName .
?region geo:asWKT ?subDistWkt .
BIND('#008AFF5C' AS ?subDistWktColor)
?building a lgdo:Residential .
#?building rdfs:label ?bldgName . ## ISSUE - if included some OSM null values are omitted 
?building geo:asWKT ?bldgWkt .
BIND('red' AS ?bldgWktColor)
FILTER (geof:sfWithin(?bldgWkt, ?subDistWkt))
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Elevation'))
BIND ('2000-02-11T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?subDistWkt, ?rasterName) AS ?elevation)
FILTER(?elevation > 520)
} 

"""

[[tabGroups.tabs]]

name="Q18. Get Residential (in 35.9 sec)"
query="""
########################################################################################################
#### Query 18. List all the residential in Munich where average temperature is more than 276 Kelvin ####
########################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>

SELECT ?distName ?temp ?distWkt ?distWktColor ?distWktLabel ?bldgWkt ?bldgWktColor { # bldgName
?region a :District .
?region rdfs:label ?distName .
?region geo:asWKT ?distWkt .
BIND(?distName AS ?distWktLabel)
BIND('#008AFF5C' AS ?distWktColor)
?building a lgdo:Residential .
#?building rdfs:label ?bldgName . ## ISSUE - if included some OSM null values are omitted 
?building geo:asWKT ?bldgWkt .
BIND('red' AS ?bldgWktColor)
FILTER (geof:sfWithin(?bldgWkt, ?distWkt))
?gridCoverage a :Raster .
?gridCoverage rasdb:rasterName ?rasterName .
FILTER (CONTAINS(?rasterName, 'Munich_MODIS_Temperature_1km')) 
BIND ('2022-01-01T00:00:00+00:00'^^xsd:dateTime AS ?timeStamp)
BIND (rasdb:rasSpatialAverage(?timeStamp, ?distWkt, ?rasterName) AS ?temp)
FILTER(?temp > 13850.0) # 277 K
} 

"""

[[tabGroups.tabs]]

name="Q19. Get 3D buildings**"
query="""
##################################################################################################################################
#### Query 19. List all the buildings in Munich where building height is 30 meters and average elevation less than 510 meters ####
##################################################################################################################################

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>



"""

[[tabGroups.tabs]]

name="Q20. Get 3D buildings**"
query="""
##############
#### Query 20. 
##############

PREFIX :	<https://github.com/aghoshpro/OntoRaster/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geo:	 <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX rasdb: <https://github.com/aghoshpro/RasterDataCube/>



"""


